plugins {
    id 'com.android.application'
}

android {
    namespace "com.popcap.pvz"
    compileSdk 33
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_PLATFORM=android-21", "-DANDROID_STL=c++_static"
                // abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                //abiFilters 'arm64-v8a', 'x86', 'x86_64'
                abiFilters 'arm64-v8a'
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            // Debug 模式：禁用优化，启用调试信息
            externalNativeBuild {
                cmake {
                    cppFlags "-O0",  // 禁用优化以便调试
                              "-g",  // 生成调试信息
                              "-DDEBUG",  // 定义 DEBUG 宏
                              "-ffp-contract=off",  // 禁用浮点融合，确保跨架构一致性
                              "-fno-strict-aliasing",  // 禁用严格别名，提高兼容性
                              "-fexceptions",  // 启用异常处理
                              "-frtti"  // 启用运行时类型信息
                }
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // Release 模式：启用优化，确保性能
            externalNativeBuild {
                cmake {
                    cppFlags "-O2",  // 平衡性能和编译时间
                              "-DNDEBUG",  // 禁用断言
                              "-ffp-contract=off",  // 禁用浮点融合，确保跨架构计算结果一致
                              "-fno-strict-aliasing",  // 禁用严格别名，提高兼容性
                              "-fexceptions",  // 启用异常处理
                              "-frtti"  // 启用运行时类型信息
                }
            }
        }
    }
    applicationVariants.all { variant ->
        tasks["merge${variant.name.capitalize()}Assets"]
            .dependsOn("externalNativeBuild${variant.name.capitalize()}")
    }
    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
        }
        externalNativeBuild {
            cmake {
                path '../../CMakeLists.txt'
                version '3.22.1' // 使用的CMake版本
            }
        }
    }
    lint {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}
